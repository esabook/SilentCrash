apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    repositories {
        maven {
            name "snapshot"
            url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username = findProperty("NEXUS_USERNAME")
                password = findProperty("NEXUS_PASSWORD")
            }
        }
        maven {
            name "staging"
            url = "https://s01.oss.sonatype.org/content/groups/staging"
            credentials {
                username = findProperty("NEXUS_USERNAME")
                password = findProperty("NEXUS_PASSWORD")
            }
        }
    }

    publications {
        release(MavenPublication) {
            pom {
                name = 'SilentCrash'
                description = 'Small lib to override default Android\'s ExceptionHandler'
                url = 'https://github.com/esabook/SilentCrash'
                licenses {
                    license {
                        name = 'The Unlicense'
                        url = 'https://github.com/esabook/SilentCrash/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'egit'
                        name = 'Egit'
                        email = 'esa_book@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/esabook/SilentCrash.git'
                    developerConnection = 'scm:git:ssh://github.com/esabook/SilentCrash.git'
                    url = 'http://github.com/esabook/SilentCrash'
                }
            }
        }
    }
}



// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'io.github.esabook'
                artifactId = 'silentcrash'
                version = findProperty("build.versionName")
            }
        }
    }
}


signing {
    sign publishing.publications.release
}